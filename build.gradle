buildscript {
    ext {
        springBootVersion = '1.5.6.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.moowork.node'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')
    runtime('org.springframework.boot:spring-boot-devtools')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

ext {
    reactAppDir = new File(projectDir, 'reactapp')
}

sourceSets {
    reactApp {
        resources.srcDirs = [new File(reactAppDir, 'build')]
        output.resourcesDir = new File(buildDir, 'resources/main/reactapp')
    }
}

node {
    download = true
    nodeModulesDir = reactAppDir
}

task yarnInstall(type: YarnTask) {
    workingDir = reactAppDir
    args = ['install']
}

task yarnStart(type: YarnTask, dependsOn: yarnInstall) {
    workingDir = reactAppDir
    args = ['start']
}

task yarnBuild(type: YarnTask, dependsOn: yarnInstall) {
    workingDir = reactAppDir
    args = ['build']
}

processReactAppResources.dependsOn yarnBuild
processResources.dependsOn processReactAppResources

